buildscript {
    repositories {
        mavenLocal()
        maven { url = 'https://files.minecraftforge.net/maven' }
        gradlePluginPortal()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:5.+'
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.4"
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

version = project.property('collar.mod.version') + '-' + getCheckedOutGitCommitHash()
group = 'team.catgirl.collar.mod'
archivesBaseName = 'collar-forge-' + project.property('collar.mc.version')
sourceCompatibility = targetCompatibility = '1.8'

minecraft {
    mappings channel: project.property('collar.mcp.channel'), version: "${project.property('collar.mcp.mapping')}-${project.property('collar.mcp.version')}"
    //makeObfSourceJar = false
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }

        server {

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'
        }
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title": "examplemod",
                "Specification-Vendor": "examplemodsareus",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${project.version}",
                "Implementation-Vendor" :"examplemodsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://libraries.minecraft.net"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.property('collar.forge.version')}"
    implementation project(':plastic-api')
    implementation project(':common')
    implementation group: 'team.catgirl', name: 'pounce', version: "${project.property('pounce.version')}"
    implementation "com.mojang:brigadier:${project.property("brigadier.version")}"
    implementation group: 'team.catgirl.collar', name: 'client', version: "${project.property('collar.version')}", classifier: 'full'
    implementation group: 'team.catgirl.collar', name: 'plugin', version: "${project.property('collar.version')}"
}

shadowJar {
    dependencies {
        include(dependency('com.mojang:brigadier:.*'))
        include(dependency('team.catgirl:pounce:.*'))
        include(project(':plastic-api'))
        include(project(':common'))
        include(dependency('team.catgirl.collar:client:.*'))
        include(dependency('team.catgirl.collar:plugin:.*'))
    }
    relocate 'com.mojang.brigadier', 'team.catgirl.collar.libs.com.mojang.brigadier'
    relocate 'team.catgirl.collar.shared', 'team.catgirl.collar.libs.team.catgirl.collar.shared'
    relocate 'team.catgirl.collar.client', 'team.catgirl.collar.libs.team.catgirl.collar.client'
    relocate 'team.catgirl.pounce', 'team.catgirl.collar.libs.team.catgirl.pounce'
    classifier ''
}

jar.finalizedBy('shadowJar','reobfJar')